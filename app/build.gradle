buildscript {
    ext.objectboxVersion = '2.3.1'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "io.objectbox:objectbox-gradle-plugin:$objectboxVersion"
        classpath 'io.realm:realm-gradle-plugin:5.8.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.objectbox'
apply plugin: 'realm-android'

android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "io.objectbox.performanceapp"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        releaseDebugCert {
            initWith release
            // Just to use without checkjni
            signingConfig signingConfigs.debug
        }
        debugJniNoDebug {
            initWith debug
            // Just to use without checkjni
            jniDebuggable false
        }
    }
}

dependencies {
    implementation "androidx.room:room-runtime:2.0.0"
    annotationProcessor "androidx.room:room-compiler:2.0.0"
    implementation "io.objectbox:objectbox-android:$objectboxVersion"
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'org.greenrobot:essentials:3.0.0-RC1'
}
